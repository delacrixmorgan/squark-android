plugins {
    id 'com.android.application'
    id 'com.google.firebase.firebase-perf'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 32
    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
    defaultConfig {
        applicationId "com.delacrixmorgan.squark"
        minSdkVersion 22
        targetSdkVersion 32
        versionCode 20
        versionName "2.5.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            splits.abi.enable = false
            splits.density.enable = false
            ext.alwaysUpdateBuildId = false
            buildConfigField 'String', "BASE_URL", '"https://api.apilayer.com/"'
            manifestPlaceholders = [appLabel: "@string/app_name_debug"]
        }
        release {
            buildConfigField 'String', "BASE_URL", '"https://api.apilayer.com/"'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [appLabel: "@string/app_name"]
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                '-opt-in=kotlin.time.ExperimentalTime',
                '-opt-in=kotlin.contracts.ExperimentalContracts',
                '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                '-opt-in=kotlinx.coroutines.FlowPreview',
                '-Xbackend-threads=4'
        ]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Upgrading to v1.2.0 will have D8 and R8 Build Error
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.6")

    implementation platform('com.google.firebase:firebase-bom:30.3.1')

    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    implementation "com.google.android.material:material:1.6.1"
    implementation 'com.google.android.play:core-ktx:1.8.1'
    implementation "com.google.code.gson:gson:2.9.0"
    implementation "com.google.dagger:hilt-android:2.42"
    kapt "com.google.dagger:hilt-compiler:2.42"

    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.fragment:fragment-ktx:1.5.2"
    implementation "androidx.appcompat:appcompat:1.5.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation "androidx.navigation:navigation-ui-ktx:2.5.1"
    implementation "androidx.navigation:navigation-fragment-ktx:2.5.1"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"

    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    implementation 'me.zhanghai.android.fastscroll:library:1.1.7'
    implementation 'io.coil-kt:coil:2.1.0'

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-beta02', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'junit:junit:4.13.2'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: "kotlinx-serialization"